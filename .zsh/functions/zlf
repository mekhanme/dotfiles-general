#!/bin/zsh
# https://github.com/gokcehan/lf/wiki/Integrations#zsh-file-picker--directory-changer

# TODO: не загружается функция через autoload
_zlf() {
    emulate -L zsh
    local d=$(mktemp -d) || return 1
    {
        mkfifo -m 600 $d/fifo || return 1
        tmux split -bf zsh -c "exec {ZLE_FIFO}>$d/fifo; export ZLE_FIFO; exec lf" || return 1
        local fd
        exec {fd}<$d/fifo
        zle -Fw $fd _zlf_handler
    } always {
        rm -rf $d
    }
}
zle -N _zlf

# [Alt - k ] открыть lf в tmux split
# нажатие a в lf добавляет выбранный файл в zsh command line как relative paths, A как absolute paths
bindkey '\ek' _zlf

_zlf_handler() {
    emulate -L zsh
    local line
    if ! read -r line <&$1; then
        zle -F $1
        exec {1}<&-
        return 1
    fi
    eval $line
    zle -R
}
zle -N _zlf_handler